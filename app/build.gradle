buildscript {
    ext.kotlin_version = '1.0.0-beta-3595'
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath 'me.tatarka:gradle-retrolambda:3.2.3'
        classpath 'io.fabric.tools:gradle:1.+'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.2.1'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.+'
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
    maven { url "https://github.com/alter-ego/Android-RSS-Reader-Library/raw/master/releases/" }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.getkeepsafe.dexcount'

def mixpanel_key = hasProperty('MIXPANEL_UNISANNIO') ? MIXPANEL_UNISANNIO : System.getenv('MIXPANEL_UNISANNIO')

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "solutions.alterego.android.unisannio"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 2
        versionName "20151214"
        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file("../../android-keystore")
            storePassword ""
            keyAlias "android"
            keyPassword ""
        }
    }

    buildTypes {
        debug {
            buildConfigField "String", "MIXPANEL_TOKEN", mixpanel_key
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            buildConfigField "String", "MIXPANEL_TOKEN", mixpanel_key
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    dexOptions {
        incremental true
        javaMaxHeapSize "4g"
    }

    dexcount {
        includeClasses = false
        includeFieldCount = false
        printAsTree = false
        orderByMethodCount = true
        verbose = false
    }

    testOptions {
        unitTests.returnDefaultValues = true

        unitTests.all {
            jvmArgs '-XX:MaxPermSize=256m'
        }
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // AlterEgo
    compile 'com.github.alter-ego:advanced-android-logger:-SNAPSHOT'

    // Google
    compile 'com.android.support:support-v4:23.1.1'
    compile "com.android.support:appcompat-v7:23.1.1"
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:multidex:1.0.1'
    compile "com.google.android.gms:play-services-maps:8.1.0"

    // Analytics
    compile "com.google.android.gms:play-services-analytics:8.1.0"
    compile "com.mixpanel.android:mixpanel-android:4.6.4"

    // Injections
    compile 'com.google.dagger:dagger:2.0.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    apt "com.google.dagger:dagger-compiler:2.0.1"
    provided 'javax.annotation:jsr250-api:1.0'

    // Annotations
    apt "org.projectlombok:lombok:1.14.8"
    provided "org.projectlombok:lombok:1.14.8"
    compile "org.parceler:parceler-api:1.0.3"
    apt "org.parceler:parceler:1.0.3"

    // Parsing
    compile 'org.jsoup:jsoup:1.7.3'
    compile 'com.alterego:androidrssreaderlibrary:1.0.0'

    // Rx
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'io.reactivex:rxandroid:1.1.0'

    //Stetho debugging
    compile 'com.facebook.stetho:stetho:1.3.0'
    compile 'com.facebook.stetho:stetho-okhttp3:1.3.0'

    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
        transitive = true;
    }

    // UI
    compile 'com.joanzapata.android:android-iconify:1.0.9'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile 'com.github.DreaminginCodeZH:CustomTabsHelper:v1.0.1'

    // Utils
    compile 'net.danlew:android.joda:2.8.2'

    // Testing
    androidTestCompile 'com.google.dexmaker:dexmaker:1.2'
    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'

    testCompile('junit:junit:4.12') {
        exclude module: 'hamcrest'
        exclude module: 'hamcrest-core'
    }
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
    testCompile('org.mockito:mockito-core:1.10.19') {
        exclude module: 'hamcrest'
        exclude module: 'hamcrest-core'
    }
    testCompile 'org.hamcrest:hamcrest-library:1.3'

    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
}
